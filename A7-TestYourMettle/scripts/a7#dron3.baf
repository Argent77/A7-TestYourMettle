// Combat script: Wizard Construct
/*
Available spells:
SPWI102   Armor
SPWI106   Blindness
SPWI112   Magic Missile
SPWI115   Shocking Grasp

SPWI201   Blur
SPWI206   Invisibility
SPWI211   Melf's Acid Arrow
SPWI212   Mirror Image

SPWI303   Flame Arrow
SPWI308   Lightning Bolt
SPWI311   Protection From Normal Missiles
SPWI325   Melf's Minute Meteors

SPWI405   Improved Invisibility
SPWI406   Minor Globe of Invulnerability
SPWI412   Greater Malison
SPWI418   Fire Shield (Red)

SPWI505   Shadow Door
SPWI513   Breach
SPWI522   Minor Spell Turning

SPWI607   Mislead
SPWI609   True Sight
SPWI615   Chain Lightning

SPWI704   Ruby Ray of Reversal
SPWI708   Mantle
*/

IF
  Global("summonTimer","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobalTimer("summonTimer","LOCALS",6)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNum(3,1)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_BLUR)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_BLUR)
    ReallyForceSpell(Myself,WIZARD_BLUR)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNum(2,1)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_FIRE_SHIELD_RED)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_FIRE_SHIELD_RED)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNumGT(5,2)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNumGT(5,2)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  !HaveSpell(WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
  HaveSpell(WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNumGT(3,1)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_MIRROR_IMAGE)
  RandomNum(2,2)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_MIRROR_IMAGE)
    ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNum(2,2)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_SHADOW_DOOR)
  !HaveSpell(WIZARD_IMPROVED_INVISIBILITY)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_SHADOW_DOOR)
    ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNum(2,2)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_IMPROVED_INVISIBILITY)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_IMPROVED_INVISIBILITY)
    ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  RandomNum(2,2)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_GLOBE_OF_INVULNERABILITY)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_GLOBE_OF_INVULNERABILITY)
    ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
  !HaveSpell(WIZARD_GLOBE_OF_INVULNERABILITY)
  RandomNum(2,2)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
    ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_MINOR_SPELL_TURNING)
  !HaveSpell(WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
  !HaveSpell(WIZARD_GLOBE_OF_INVULNERABILITY)
  RandomNum(2,2)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_MINOR_SPELL_TURNING)
    ReallyForceSpell(Myself,WIZARD_MINOR_SPELL_TURNING)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  HaveSpell(WIZARD_MINOR_SPELL_DEFLECTION)
  !HaveSpell(WIZARD_MINOR_SPELL_TURNING)
  !HaveSpell(WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
  !HaveSpell(WIZARD_GLOBE_OF_INVULNERABILITY)
  RandomNum(2,2)
THEN
  RESPONSE #100
    RemoveSpell(WIZARD_MINOR_SPELL_DEFLECTION)
    ReallyForceSpell(Myself,WIZARD_MINOR_SPELL_DEFLECTION)
    Continue()
END

IF
  DifficultyGT(EASIEST)
  Global("prebuff","LOCALS",0)
  !See([0.0.MODRON])
  Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("prebuff","LOCALS",1)
    SetGlobalTimer("defence_timer","LOCALS",4)
    DisplayString(Myself,39969)  // Spell Sequencerâ€”Fired
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_VOCALIZE)
  StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_VOCALIZE)
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  CheckStat(Myself,0,STONESKINS)
  !See([0.0.MODRON])
  HitBy([ANYONE],CRUSHING)
  !HitBy([ANYONE],ACID)
  !HitBy([ANYONE],COLD)
  !HitBy([ANYONE],ELECTRICITY)
  !HitBy([ANYONE],FIRE)
  !HitBy([ANYONE],POISON)
  HaveSpell(WIZARD_MANTLE)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_MANTLE)
  RESPONSE #30
    Continue()
END

IF
  DifficultyGT(EASIEST)
  DifficultyLT(HARD)
  GlobalTimerExpired("summonTimer","LOCALS")
  See([GOODCUTOFF])
THEN
  RESPONSE #50
    SetGlobalTimerRandom("summonTimer","LOCALS",24,60)
    ForceSpellRES("A7#SUMD",Myself)
  RESPONSE #50
    SetGlobalTimerRandom("summonTimer","LOCALS",24,60)
    Continue()
END

IF
  DifficultyGT(NORMAL)
  !NightmareModeOn()
  GlobalTimerExpired("summonTimer","LOCALS")
  See([GOODCUTOFF])
THEN
  RESPONSE #75
    SetGlobalTimerRandom("summonTimer","LOCALS",12,24)
    ForceSpellRES("A7#SUMD",Myself)
  RESPONSE #25
    SetGlobalTimerRandom("summonTimer","LOCALS",12,24)
    Continue()
END

IF
  NightmareModeOn()
  GlobalTimerExpired("summonTimer","LOCALS")
  See([GOODCUTOFF])
THEN
  RESPONSE #75
    SetGlobalTimerRandom("summonTimer","LOCALS",12,24)
    ForceSpellRES("A7#SUMD",Myself)
END

IF
  Global("spacewarp","LOCALS",0)
  !DifficultyGT(NORMAL)
  See([GOODCUTOFF])
  NearLocation(LastSeenBy(Myself),530,550,8)
  TriggerOverride(LastSeenBy(Myself),Range(NearestAllyOf(Myself),10))
  TriggerOverride(LastSeenBy(Myself),Range(SecondNearestAllyOf(Myself),10))
THEN
  RESPONSE #25
    SetGlobal("spacewarp","LOCALS",1)
    ForceSpellRES("A7#SPW1",LastSeenBy(Myself))   // Spacewarp
  RESPONSE #75
    SetGlobal("spacewarp","LOCALS",1)
    Continue()
END

IF
  Global("spacewarp","LOCALS",0)
  DifficultyGT(NORMAL)
  !NightmareModeOn()
  See([GOODCUTOFF])
  NearLocation(LastSeenBy(Myself),530,550,8)
  TriggerOverride(LastSeenBy(Myself),Range(NearestAllyOf(Myself),10))
  TriggerOverride(LastSeenBy(Myself),Range(SecondNearestAllyOf(Myself),10))
THEN
  RESPONSE #100
    SetGlobal("spacewarp","LOCALS",1)
    ForceSpellRES("A7#SPW1",LastSeenBy(Myself))   // Spacewarp
END

IF
  !GlobalTimerNotExpired("SpacewarpTimer","LOCALS")
  NightmareModeOn()
  See([GOODCUTOFF])
  NearLocation(LastSeenBy(Myself),530,550,8)
  TriggerOverride(LastSeenBy(Myself),Range(NearestAllyOf(Myself),10))
  TriggerOverride(LastSeenBy(Myself),Range(SecondNearestAllyOf(Myself),10))
THEN
  RESPONSE #100
    SetGlobalTimerRandom("SpacewarpTimer","LOCALS",48, 72)
    ForceSpellRES("A7#SPW1",LastSeenBy(Myself))   // Spacewarp
END

IF
  HasItem("MELFMET",Myself)
  OR(7)
    See([0.0.MODRON])
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(FourthNearestEnemyOf(Myself))
    See(FifthNearestEnemyOf(Myself))
    See(SixthNearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),4)
THEN
  RESPONSE #100
    RunAwayFromNoInterrupt(LastSeenBy(Myself),30)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  HasItem("MELFMET",Myself)
  OR(7)
    See([0.0.MODRON])
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(FourthNearestEnemyOf(Myself))
    See(FifthNearestEnemyOf(Myself))
    See(SixthNearestEnemyOf(Myself))
  !Range(LastSeenBy(Myself),4)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  CheckStat(Myself,0,STONESKINS)
  !See([0.0.MODRON])
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_STONE_SKIN)
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_STONE_SKIN)
  RESPONSE #30
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  CheckStat(Myself,0,STONESKINS)
  !See([0.0.MODRON])
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MIRROR_IMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_MIRROR_IMAGE)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !CheckSpellState(Myself,BUFF_PRO_WEAPONS)
  !See([0.0.MODRON])
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !CheckSpellState(Myself,BUFF_PRO_WEAPONS)
  !See([0.0.MODRON])
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !CheckSpellState(Myself,BUFF_PRO_SPELLS)
  HaveSpell(WIZARD_MINOR_SPELL_TURNING)
  !See([0.0.MODRON])
  OR(4)
    See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([0.0.0.BARD_ALL]))
    See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_MINOR_SPELL_TURNING)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !CheckSpellState(Myself,BUFF_PRO_SPELLS)
  HaveSpell(WIZARD_GLOBE_OF_INVULNERABILITY)
  !See([0.0.MODRON])
  OR(4)
    See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([0.0.0.BARD_ALL]))
    See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !CheckSpellState(Myself,BUFF_PRO_SPELLS)
  HaveSpell(WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
  !See([0.0.MODRON])
  OR(4)
    See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([0.0.0.BARD_ALL]))
    See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !CheckSpellState(Myself,CANNOT_TURN_INVISIBLE)
  !See([0.0.MODRON])
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MISLEAD)
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_MISLEAD)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !CheckSpellState(Myself,CANNOT_TURN_INVISIBLE)
  !See([0.0.MODRON])
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_SHADOW_DOOR)
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_SHADOW_DOOR)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  !GlobalTimerNotExpired("defence_timer","LOCALS")
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !CheckSpellState(Myself,CANNOT_TURN_INVISIBLE)
  !See([0.0.MODRON])
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_IMPROVED_INVISIBILITY)
THEN
  RESPONSE #100
    SetGlobalTimer("defence_timer","LOCALS",THREE_MINUTES)
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_IMPROVED_INVISIBILITY)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  CheckStat(Myself,0,TRUE_SIGHT)
  OR(2)
    HaveSpell(WIZARD_TRUE_SIGHT)
    HaveSpell(WIZARD_ORACLE)
  OR(2)
    !Detect(NearestEnemyOf(Myself))
    !StateCheck(NearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(SecondNearestEnemyOf(Myself))
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(ThirdNearestEnemyOf(Myself))
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(FourthNearestEnemyOf(Myself))
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(FifthNearestEnemyOf(Myself))
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(SixthNearestEnemyOf(Myself))
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(SeventhNearestEnemyOf(Myself))
    !StateCheck(SeventhNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(EighthNearestEnemyOf(Myself))
    !StateCheck(EighthNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(NinthNearestEnemyOf(Myself))
    !StateCheck(NinthNearestEnemyOf(Myself),STATE_ILLUSIONS)
  OR(2)
    !Detect(TenthNearestEnemyOf(Myself))
    !StateCheck(TenthNearestEnemyOf(Myself),STATE_ILLUSIONS)
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  CheckStat(Myself,0,TRUE_SIGHT)
  HaveSpell(WIZARD_TRUE_SIGHT)
  // !See([0.0.MODRON])
  Detect(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  StateCheck(LastSeenBy,STATE_ILLUSIONS)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_TRUE_SIGHT)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  CheckStat(Myself,0,TRUE_SIGHT)
  HaveSpell(WIZARD_ORACLE)
  // !See([0.0.MODRON])
  Detect(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  StateCheck(LastSeenBy,STATE_ILLUSIONS)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_ORACLE)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  CheckStat(Myself,0,TRUE_SIGHT)
  Allegiance(Myself,EVILCUTOFF)
  // !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  !See(LastSeenBy(Myself))
  HaveSpell(WIZARD_DETECT_INVISIBILITY)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_DETECT_INVISIBILITY)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  CheckStat(Myself,0,TRUE_SIGHT)
  Allegiance(Myself,EVILCUTOFF)
  // !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  !See(LastSeenBy(Myself))
  HaveSpell(WIZARD_TRUE_SIGHT)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_TRUE_SIGHT)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  CheckStat(Myself,0,TRUE_SIGHT)
  Allegiance(Myself,EVILCUTOFF)
  // !See([0.0.MODRON])
  Detect([GOODCUTOFF])
  !See(LastSeenBy(Myself))
  HaveSpell(WIZARD_ORACLE)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_ORACLE)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  HaveSpell(WIZARD_BREACH)
  OR(2)
    !See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    !CheckStatGT(NearestEnemyOfType([0.0.0.MAGE_ALL]),0,STONESKINS)
  OR(2)
    !See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
    !CheckSpellState(NearestEnemyOfType([0.0.0.MAGE_ALL]),BUFF_PRO_WEAPONS)
  OR(2)
    !See(SecondNearestEnemyOfType([0.0.0.MAGE_ALL]))
    !CheckStatGT(SecondNearestEnemyOfType([0.0.0.MAGE_ALL]),0,STONESKINS)
  OR(2)
    !See(SecondNearestEnemyOfType([0.0.0.MAGE_ALL]))
    !CheckSpellState(SecondNearestEnemyOfType([0.0.0.MAGE_ALL]),BUFF_PRO_WEAPONS)
  OR(2)
    !See(ThirdNearestEnemyOfType([0.0.0.MAGE_ALL]))
    !CheckStatGT(ThirdNearestEnemyOfType([0.0.0.MAGE_ALL]),0,STONESKINS)
  OR(2)
    !See(ThirdNearestEnemyOfType([0.0.0.MAGE_ALL]))
    !CheckSpellState(ThirdNearestEnemyOfType([0.0.0.MAGE_ALL]),BUFF_PRO_WEAPONS)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !CheckStatGT(NearestEnemyOf(Myself),0,STONESKINS)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !CheckSpellState(NearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !CheckStatGT(SecondNearestEnemyOf(Myself),0,STONESKINS)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !CheckSpellState(SecondNearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !CheckStatGT(ThirdNearestEnemyOf(Myself),0,STONESKINS)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !CheckSpellState(ThirdNearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_BREACH)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  OR(2)
    CheckSpellState(LastSeenBy,BUFF_PRO_WEAPONS)
    CheckStatGT(LastSeenBy,0,STONESKINS)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_BREACH)
  RESPONSE #100
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  HaveSpell(WIZARD_BREACH)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !CheckSpellState(NearestEnemyOf(Myself),BUFF_PRO_DAMAGE)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !CheckSpellState(NearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !CheckSpellState(SecondNearestEnemyOf(Myself),BUFF_PRO_DAMAGE)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !CheckSpellState(SecondNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !CheckSpellState(ThirdNearestEnemyOf(Myself),BUFF_PRO_DAMAGE)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !CheckSpellState(ThirdNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(FourthNearestEnemyOf(Myself))
    !CheckSpellState(FourthNearestEnemyOf(Myself),BUFF_PRO_DAMAGE)
  OR(2)
    !See(FourthNearestEnemyOf(Myself))
    !CheckSpellState(FourthNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(FifthNearestEnemyOf(Myself))
    !CheckSpellState(FifthNearestEnemyOf(Myself),BUFF_PRO_DAMAGE)
  OR(2)
    !See(FifthNearestEnemyOf(Myself))
    !CheckSpellState(FifthNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_BREACH)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  OR(2)
    CheckSpellState(LastSeenBy,BUFF_PRO_EFFECTS)
    CheckSpellState(LastSeenBy,BUFF_PRO_DAMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_BREACH)
  RESPONSE #100
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  OR(4)
    HaveSpell(WIZARD_RUBY_RAY_OF_REVERSAL)
    HaveSpell(WIZARD_PIERCE_MAGIC)
    HaveSpell(WIZARD_SECRET_WORD)
    HaveSpell(WIZARD_SPELL_THRUST)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !CheckSpellState(NearestEnemyOf(Myself),BUFF_PRO_SPELLS)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !CheckSpellState(SecondNearestEnemyOf(Myself),BUFF_PRO_SPELLS)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !CheckSpellState(ThirdNearestEnemyOf(Myself),BUFF_PRO_SPELLS)
  OR(2)
    !See(FourthNearestEnemyOf(Myself))
    !CheckSpellState(FourthNearestEnemyOf(Myself),BUFF_PRO_SPELLS)
  OR(2)
    !See(FifthNearestEnemyOf(Myself))
    !CheckSpellState(FifthNearestEnemyOf(Myself),BUFF_PRO_SPELLS)
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_RUBY_RAY_OF_REVERSAL)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  CheckSpellState(LastSeenBy,BUFF_PRO_SPELLS)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_RUBY_RAY_OF_REVERSAL)
  RESPONSE #100
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_PIERCE_MAGIC)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  CheckSpellState(LastSeenBy,BUFF_PRO_SPELLS)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_PIERCE_MAGIC)
  RESPONSE #100
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_SECRET_WORD)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  CheckSpellState(LastSeenBy,BUFF_PRO_SPELLS)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_SECRET_WORD)
  RESPONSE #100
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_SPELL_THRUST)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  CheckSpellState(LastSeenBy,BUFF_PRO_SPELLS)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_SPELL_THRUST)
  RESPONSE #100
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  HaveSpell(WIZARD_DISPEL_MAGIC)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !StateCheck(NearestEnemyOf(Myself),STATE_HASTED)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !CheckSpellState(NearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !CheckSpellState(NearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_HASTED)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !CheckSpellState(SecondNearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !CheckSpellState(SecondNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_HASTED)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !CheckSpellState(ThirdNearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !CheckSpellState(ThirdNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(FourthNearestEnemyOf(Myself))
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_HASTED)
  OR(2)
    !See(FourthNearestEnemyOf(Myself))
    !CheckSpellState(FourthNearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(FourthNearestEnemyOf(Myself))
    !CheckSpellState(FourthNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(FifthNearestEnemyOf(Myself))
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_HASTED)
  OR(2)
    !See(FifthNearestEnemyOf(Myself))
    !CheckSpellState(FifthNearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(FifthNearestEnemyOf(Myself))
    !CheckSpellState(FifthNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  OR(2)
    !See(SixthNearestEnemyOf(Myself))
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_HASTED)
  OR(2)
    !See(SixthNearestEnemyOf(Myself))
    !CheckSpellState(SixthNearestEnemyOf(Myself),BUFF_PRO_WEAPONS)
  OR(2)
    !See(SixthNearestEnemyOf(Myself))
    !CheckSpellState(SixthNearestEnemyOf(Myself),BUFF_PRO_EFFECTS)
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_DISPEL_MAGIC)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  OR(3)
    CheckSpellState(LastSeenBy,BUFF_PRO_WEAPONS)
    CheckSpellState(LastSeenBy,BUFF_PRO_EFFECTS)
    StateCheck(LastSeenBy,STATE_HASTED)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_DISPEL_MAGIC)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  HaveSpell(WIZARD_GREATER_MALISON)
  OR(2)
    !See(NearestEnemyOf(Myself))
    CheckSpellState(NearestEnemyOf(Myself),MALISON)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    CheckSpellState(SecondNearestEnemyOf(Myself),MALISON)
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    CheckSpellState(ThirdNearestEnemyOf(Myself),MALISON)
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_GREATER_MALISON)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  !CheckSpellState(LastSeenBy,MALISON)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_GREATER_MALISON)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  HaveSpell(WIZARD_BLINDNESS)
  OR(3)
    !See(NearestEnemyOfType([0.0.0.FIGHTER_ALL]))
    RandomNumGT(4,2)
    StateCheck(NearestEnemyOfType([0.0.0.FIGHTER_ALL]),STATE_DEBUFF)
  OR(3)
    !See(SecondNearestEnemyOfType([0.0.0.FIGHTER_ALL]))
    RandomNumGT(4,3)
    StateCheck(SecondNearestEnemyOfType([0.0.0.FIGHTER_ALL]),STATE_DEBUFF)
  OR(2)
    !See(ThirdNearestEnemyOfType([0.0.0.FIGHTER_ALL]))
    StateCheck(ThirdNearestEnemyOfType([0.0.0.FIGHTER_ALL]),STATE_DEBUFF)
  OR(2)
    !See(NearestEnemyOfType([0.0.0.FIGHTER_ALL]))
    StateCheck(NearestEnemyOfType([0.0.0.FIGHTER_ALL]),STATE_DEBUFF)
  OR(2)
    !See(SecondNearestEnemyOfType([0.0.0.FIGHTER_ALL]))
    StateCheck(SecondNearestEnemyOfType([0.0.0.FIGHTER_ALL]),STATE_DEBUFF)
  OR(3)
    !See(NearestEnemyOfType([0.0.0.RANGER_ALL]))
    RandomNumGT(4,2)
    StateCheck(NearestEnemyOfType([0.0.0.RANGER_ALL]),STATE_DEBUFF)
  OR(3)
    !See(SecondNearestEnemyOfType([0.0.0.RANGER_ALL]))
    RandomNumGT(4,3)
    StateCheck(SecondNearestEnemyOfType([0.0.0.RANGER_ALL]),STATE_DEBUFF)
  OR(2)
    !See(ThirdNearestEnemyOfType([0.0.0.RANGER_ALL]))
    StateCheck(ThirdNearestEnemyOfType([0.0.0.RANGER_ALL]),STATE_DEBUFF)
  OR(2)
    !See(NearestEnemyOfType([0.0.0.RANGER_ALL]))
    StateCheck(NearestEnemyOfType([0.0.0.RANGER_ALL]),STATE_DEBUFF)
  OR(2)
    !See(SecondNearestEnemyOfType([0.0.0.RANGER_ALL]))
    StateCheck(SecondNearestEnemyOfType([0.0.0.RANGER_ALL]),STATE_DEBUFF)
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_BLINDNESS)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  !StateCheck(LastSeenBy,STATE_DEBUFF)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_BLINDNESS)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  !Range([0.0.MODRON],30)
  HaveSpell(WIZARD_CHAIN_LIGHTNING)
  OR(2)
    !See(NearestEnemyOf(Myself))
    !TriggerOverride(NearestEnemyOf(Myself),Range(NearestAllyOf(Myself),13))
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    !TriggerOverride(SecondNearestEnemyOf(Myself),Range(NearestAllyOf(Myself),13))
  OR(2)
    !See(ThirdNearestEnemyOf(Myself))
    !TriggerOverride(ThirdNearestEnemyOf(Myself),Range(NearestAllyOf(Myself),13))
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_CHAIN_LIGHTNING)
  // !See([0.0.MODRON])
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
  TriggerOverride(LastSeenBy,Range(NearestAllyOf(Myself),13))
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_CHAIN_LIGHTNING)
  RESPONSE #50
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  Detect(Myself)
  OR(6)
    HaveSpell(WIZARD_FLAME_ARROW)
    HaveSpell(WIZARD_LIGHTNING_BOLT)
    HaveSpell(WIZARD_MELF_ACID_ARROW)
    HaveSpell(WIZARD_MAGIC_MISSILE)
    HaveSpell(WIZARD_CHROMATIC_ORB)
    HaveSpell(WIZARD_MELF_METEOR)
  !See([0.0.MODRON])
  OR(2)
    !See(NearestEnemyOf(Myself))
    RandomNumGT(4,2)
  OR(2)
    !See(SecondNearestEnemyOf(Myself))
    RandomNumGT(4,3)
  !See(ThirdNearestEnemyOf(Myself))
  !See(NearestEnemyOf(Myself))
  !See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_FLAME_ARROW)
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_FLAME_ARROW)
  RESPONSE #25
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_LIGHTNING_BOLT)
  See(LastSeenBy)
  !Range(LastSeenBy,4)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_LIGHTNING_BOLT)
  RESPONSE #25
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_MELF_ACID_ARROW)
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_MELF_ACID_ARROW)
  RESPONSE #25
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_MAGIC_MISSILE)
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_MAGIC_MISSILE)
  RESPONSE #25
    Continue()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_CHROMATIC_ORB)
  See(LastSeenBy)
  !Range(LastSeenBy,0)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(LastSeenBy,WIZARD_CHROMATIC_ORB)
  RESPONSE #25
    Continue()
END

IF
  DifficultyGT(EASIEST)
  !GlobalTimerNotExpired("cast","LOCALS")
  HaveSpell(WIZARD_MELF_METEOR)
  !HasItem("MELFMET",Myself)
  See(LastSeenBy)
  !Range(LastSeenBy,4)
  !StateCheck(LastSeenBy,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_MELF_METEOR)
  RESPONSE #25
    Continue()
END

IF
  !GlobalTimerExpired("cast","LOCALS")
  !StateCheck(Myself,STATE_INVISIBLE)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),8)
  !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_BLIND)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFromNoInterrupt(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
  RESPONSE #25
    Continue()
END
